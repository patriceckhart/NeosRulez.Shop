prototype(NeosRulez.Shop:Content.Products) < prototype(Neos.Neos:ContentComponent) {
    selection = Neos.Neos:MenuItems {
        itemCollection = ${q(node).property('selection')}
    }
    selection.@if.condition = ${q(node).property('selection') && Array.length(q(node).property('selection')) > 0 ? true : false}
    startingPoint = Neos.Neos:MenuItems {
        startingPoint = ${q(node).property('startingPoint')}
        filter = 'NeosRulez.Shop:Document.Product'
    }
    selection.@if.condition = ${q(node).property('startingPoint') ? true : false}
    products = ${this.startingPoint ? this.startingPoint : this.selection}

    renderer = afx`
        <NeosRulez.Shop:Component.AbstractContent>
            <div @if.render={props.products && Array.length(props.products) > 0} class="products filtered">
                <div class="row">
                    <Neos.Fusion:Loop items={props.products} item="item">
                        <NeosRulez.Shop:Content.Products.Item />
                    </Neos.Fusion:Loop>
                </div>
            </div>
        </NeosRulez.Shop:Component.AbstractContent>
    `
    @cache {
        mode = 'dynamic'
        entryIdentifier {
            node = ${node}
        }
        context {
            1 = 'node'
            2 = 'documentNode'
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTag(documentNode)}
        }
    }
}

prototype(NeosRulez.Shop:Content.Products.Item) < prototype(Neos.Fusion:Component) {

    cropImage = ${q(node).property('cropImage')}
    useImageSourceSet = ${Configuration.setting('NeosRulez.Shop.useImageSourceSet')}

    renderer = afx`
        <div class={'col-md-4 mix' + (q(item.node).property('categories')[0].identifier ? ' category-' + q(item.node).property('categories')[0].identifier : '')}>
            <Neos.Neos:NodeLink node={item.node} attributes.class="d-block product-item-link">
                <div class="row">
                    <div class="col-12">
                        <Neos.Fusion:Loop @if.render={item.node.properties.images && Array.length(item.node.properties.images) > 0} items={item.node.properties.images} itemName="asset" itemKey="assetKey">
                            <Neos.Neos:ImageTag @if.render={assetKey == 0 && !props.useImageSourceSet} asset={asset} allowUpScaling="true" allowCropping={props.cropImage ? true : false} width="400" height={props.cropImage ? 280 : false} attributes.class="img-fluid product-item-image" async={true} />
                            <NeosRulez.Bootstrap:Component.ImageTag @if.render={assetKey == 0 && props.useImageSourceSet} image={asset} width="400" height={props.cropImage ? 280 : false} allowCropping={props.cropImage ? true : false} allowUpScaling={true} async={true} />
                        </Neos.Fusion:Loop>
                        <div class="d-block mt-2 product-item-title">
                            {item.node.properties.title}
                        </div>
                    </div>
                </div>
                <div class="row align-items-end">
                    <div class="col-5">
                        <small class="product-item-articlenumber">Art.Nr.: {item.node.properties.article_number}</small>
                    </div>
                    <div class="col-7 text-end product-item-price">
                        â‚¬ {item.node.properties.price}
                    </div>
                </div>
            </Neos.Neos:NodeLink>
        </div>
    `
}
